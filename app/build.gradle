plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'androidx.navigation.safeargs.kotlin'
}

ext.versionMajor = 1
ext.versionMinor = 0
ext.versionPatch = 3
ext.versionClassifier = null
ext.isRc = true
ext.minimumSdkVersion = 19

android {
    compileSdkVersion 32

    defaultConfig {
        applicationId "id.myeco.myeco"
        minSdkVersion project.ext.minimumSdkVersion
        targetSdkVersion 32
        versionCode generateVersionCode()
        versionName generateVersionName()
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures{
        viewBinding true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildToolsVersion '33.0.0 rc4'
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.6.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.4.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.4.2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "androidx.core:core-ktx:1.7.0"

    //lifecycle
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.4.1"
    implementation "androidx.lifecycle:lifecycle-common-java8:2.4.1"

    //retrofit, gson
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    implementation "com.squareup.retrofit2:converter-scalars:2.9.0"
    implementation "com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.5"

    //coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.0-native-mt'

    //jwt decoder
    implementation 'com.auth0.android:jwtdecode:2.0.1'

    // Koin
    implementation "io.insert-koin:koin-core:2.2.3"
    implementation "io.insert-koin:koin-android:2.2.3"
    implementation "io.insert-koin:koin-android-viewmodel:2.2.3"

    // RxJava
    implementation 'io.reactivex.rxjava3:rxjava:3.1.3'
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava3:2.9.0'
    implementation "androidx.lifecycle:lifecycle-reactivestreams-ktx:2.4.1"

    //google play
    implementation "com.google.android.gms:play-services-location:19.0.1"

    //multidex
    implementation 'androidx.multidex:multidex:2.0.1'

    //test unit
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

//to generate version code
private Integer generateVersionCode() {
    return ext.minimumSdkVersion * 10000000 + ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

//to generate version name
private String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    if (ext.versionClassifier == null && ext.isRc) {
        ext.versionClassifier = "rc"
    }

    if (ext.versionClassifier != null) {
        versionName += "-" + ext.versionClassifier
    }

    return versionName
}